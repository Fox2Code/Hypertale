plugins {
    id 'maven-publish'
    id 'net.ltgt.errorprone'
}

final File hypertaleGradleDirectory = new File(project.gradle.gradleUserHomeDir, "hypertale")
final File workInProgress = new File(hypertaleGradleDirectory, "HytaleServerWIP.jar")
final File hypertaleInitJar = project(":init").layout.buildDirectory
        .file("libs/init-${project['hypertale.version']}.jar").get().asFile
// Improve compatibility with IntellijIDEA
if (!hypertaleInitJar.getParentFile().isDirectory() &&
        !hypertaleInitJar.getParentFile().mkdirs()) {
    throw new RuntimeException("Failed to create init \"build/libs\" folder!")
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
    withSourcesJar()
    withJavadocJar()
}

configurations {
    javaAgent {
        transitive = false
        canBeConsumed = false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(files(workInProgress))
    final String ASM_VERSION = project['asm.version']
    api "org.ow2.asm:asm-util:${ASM_VERSION}"
    api "org.ow2.asm:asm-commons:${ASM_VERSION}"
    api "it.unimi.dsi:fastutil:${project['fastutil.version']}"
    api "com.google.guava:guava:${project['guava.version']}"
    api "com.google.guava:failureaccess:${project['guava-failureaccess.version']}"
    errorprone "com.google.errorprone:error_prone_core:${project['errorprone.version']}"
    include(project(":patcher"))
    // We may not have init at runtime!
    compileOnly(project(":init"))

    javaAgent("net.bytebuddy:byte-buddy-agent:${project['bytebuddy.version']}")
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly(project(":patcher")) { transitive = false }
}

// Using filesMatching in processResources during import confuse IntellijIDEA
if (!Boolean.getBoolean("idea.sync.active")) {
    processResources {
        filesMatching("manifest.json") {
            expand(version: project.version)
        }
    }
}

jar {
    archiveBaseName = "Hypertale"
    manifest {
        attributes 'Hypertale-Version': project['hypertale.version']
        attributes 'Hypertale-Init': project['hypertale.init']
        attributes 'Main-Class': 'com.fox2code.hypertale.launcher.Main'
        attributes 'Launcher-Agent-Class': 'com.fox2code.hypertale.launcher.HypertaleAgent'
        attributes 'Premain-Class': 'com.fox2code.hypertale.launcher.HypertaleAgent'
        attributes 'Can-Set-Native-Method-Prefix': 'true'
        attributes 'Can-Retransform-Classes': 'true'
        attributes 'Can-Redefine-Classes': 'true'
    }
    from(hypertaleInitJar)
}

tasks.register("runServer", JavaExec) {
    classpath = sourceSets.main.runtimeClasspath + project(":patcher").sourceSets.main.output
    workingDir(new File(rootProject.rootDir, "run"))
    jvmArgs(configurations.javaAgent.collect { "-javaagent:" + it.path })
    mainClass = "com.fox2code.hypertale.launcher.Main"
    standardInput = System.in
    args("--launch-dev")
}

compileJava {
    dependsOn(":init:jar")
    dependsOn(":patcher:jar")
    dependsOn(":patcher:checkPatchHytale")
    mustRunAfter(":patcher:patchHytale")
}

test {
    useJUnitPlatform()
}