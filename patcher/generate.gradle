sourceSets {
    main {
        java {
            srcDir 'build/generated/sources/hypertale'
        }
    }
}

final Project projectCst = project
final File buildConfigSrc = layout.buildDirectory.file(
        "generated/sources/hypertale/com/fox2code/hypertale/launcher/BuildConfig.java").get().asFile

static void appendField(PrintStream printStream, String key, String value) {
    printStream.println("    public static final String " + key + " = deInline(\"" + value + "\");")
}

static void generateBuildConfig0(File buildConfigSrc, Project project) {
    buildConfigSrc.getParentFile().mkdirs()
    // Read property before trying to write file
    final String HYPERTALE_VERSION = project['hypertale.version']
    final String ERRORPRONE_VERSION = project['errorprone.version']
    final String ASM_VERSION = project['asm.version']
    final String FASTUTIL_VERSION = project['fastutil.version']
    final String GSON_VERSION = project['gson.version']
    final String GUAVA_VERSION = project['guava.version']
    final String GUAVA_FAILURE_ACCESS_VERSION = project['guava-failureaccess.version']
    final String VINEFLOWER_VERSION = project['vineflower.version']
    // Only write file if we got all the properties to not write a corrupted BuildConfig
    FileOutputStream fileOutputStream = new FileOutputStream(buildConfigSrc)
    try {
        PrintStream printStream = new PrintStream(fileOutputStream)
        printStream.println("// Auto generated class, do not modify.")
        printStream.println("package com.fox2code.hypertale.launcher;")
        printStream.println()
        printStream.println("public final class BuildConfig {")
        printStream.println("    private BuildConfig() {}")
        printStream.println("    private static String deInline(String str) { return str; }")
        appendField(printStream, 'HYPERTALE_VERSION', HYPERTALE_VERSION)
        appendField(printStream, 'ERRORPRONE_VERSION', ERRORPRONE_VERSION)
        appendField(printStream, 'ASM_VERSION', ASM_VERSION)
        appendField(printStream, 'FASTUTIL_VERSION', FASTUTIL_VERSION)
        appendField(printStream, 'GSON_VERSION', GSON_VERSION)
        appendField(printStream, 'GUAVA_VERSION', GUAVA_VERSION)
        appendField(printStream, 'GUAVA_FAILURE_ACCESS_VERSION', GUAVA_FAILURE_ACCESS_VERSION)
        appendField(printStream, 'VINEFLOWER_VERSION', VINEFLOWER_VERSION)
        printStream.println("}")
    } finally {
        fileOutputStream.close()
    }
}

tasks.register('generateBuildConfig') {
    doLast {
        generateBuildConfig0(buildConfigSrc, projectCst)
    }
}

if (!buildConfigSrc.exists()) {
    generateBuildConfig0(buildConfigSrc, projectCst)
}

compileJava.dependsOn('generateBuildConfig')